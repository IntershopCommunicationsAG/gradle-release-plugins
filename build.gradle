
plugins {
    // project plugins
    id 'java-gradle-plugin'
    id 'groovy'
    // test coverage
    id 'jacoco'

    // ide plugin
    id 'idea'

    // intershop test plugin
    id 'com.intershop.gradle.test' version '2.2.0'

    // publish plugin
    id 'maven-publish'

    // artifact signing - necessary on Maven Central
    id 'signing'

    // intershop version plugin
    id 'com.intershop.gradle.scmversion' version '2.7.4'

    // plugin for publishing to Gradle Portal
    id 'com.gradle.plugin-publish' version '0.20.0'
}

scm {
    version {
        initialVersion = '1.0.0'
    }
}

group 'com.intershop.gradle.publish'
description 'Gradle publish plugins'
version = scm.version.version

// plugins should work with JDK 1.7 aslong
// older INTERSHOP version use this version of JDK
sourceCompatibility = 1.8
targetCompatibility = 1.8

// set correct project status
if (project.version.endsWith('-SNAPSHOT')) {
    status = 'snapshot'
}

configurations {
    documentation
    artifactory
}

test {
    // Gradle versions for test
    systemProperty 'intershop.gradle.versions', '2.11'
}

jar {
    from {
        configurations.artifactory.collect { it.isDirectory() ? it : zipTree(it)}
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

// additional sources artifact
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourceJar
    archives groovydocJar
}

publishing {
    publications {
        intershopMvn(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact groovydocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode();
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', "https:/gitlab.intershop.de/IntershopGradlePlugins/${project.name}")

                def organisation = root.appendNode( 'organization' )
                organisation.appendNode("name", "Intershop Communications AG")
                organisation.appendNode("url", "http://intershop.com")

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', "https://github.com/IntershopCommunicationsAG/${project.name}")
                scm.appendNode( 'connection', "git@github.com:IntershopCommunicationsAG/${project.name}.git")
                scm.appendNode( 'developerConnection', "git@github.com:IntershopCommunicationsAG/${project.name}.git")

                def license = root.appendNode( 'licenses' ).appendNode( 'license' )
                license.appendNode( 'name', 'Apache License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode( 'distribution', 'repo' )

                def developer = root.appendNode('developers').appendNode( 'developer' )
                developer.appendNode( 'id', 'm-raab' )
                developer.appendNode( 'name', 'M. Raab' )
                developer.appendNode( 'email', 'mraab@intershop.de' )

                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.(jar)\.asc$/
                    def ref_matcher = it.file =~ /\.html\.asc$/
                    def doc_matcher = it.file =~ /\.xml\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                        extension = "jar.asc"
                    } else {
                        if(ref_matcher.find()) {
                            classifier = 'reference'
                            extension = 'html.asc'
                        }
                        else if(doc_matcher.find()) {
                            classifier = 'docbook'
                            extension = 'xml.asc'
                        } else {
                            classifier = null
                            extension = 'jar.asc'
                        }
                    }

                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign configurations.archives, configurations.documentation
}

pluginBundle {
    website = "https://github.com/IntershopCommunicationsAG/${project.name}"
    vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
    description = project.description
    tags = ['intershop', 'gradle', 'plugin', 'publish', 'release']

    plugins {
        escrowPlugin {
            id = 'com.intershop.gradle.escrow-plugin'
            displayName = 'Plugin for escrow package creation'
        }
        javadocPlugin {
            id = 'com.intershop.gradle.javadoc-configuration'
            displayName = 'Plugin with Intershop specific javadoc configuration'
        }
        javasourcePlugin {
            id = 'com.intershop.gradle.javasource-plugin'
            displayName = 'Plugin with Intershop specific javasource package configuration'
        }
        simplepublishPlugin {
            id = 'com.intershop.gradle.simplepublish-configuration'
            displayName = 'Plugin with simple publishing configuration'
        }
        artifactorypublishPlugin {
            id = 'com.intershop.gradle.artifactorypublish-configuration'
            displayName = 'Plugin with Intershop specific publishing configuration with Artifactory'
        }
        simpleartifactorypublishPlugin {
            id = 'com.intershop.gradle.simpleartifactorypublish-configuration'
            displayName = 'Plugin with Intershop specific simple publishing configuration with Artifactory'
        }
    }
}

dependencies {
    compile 'com.intershop.gradle.repoconfig:repoconfig-gradle-plugin:3.3.2'
    compile 'com.intershop.gradle.buildinfo:buildinfo-gradle-plugin:2.4.2'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4'){
        exclude group: 'org.codehaus.groovy'
    }

    // mock webserver
    testCompile 'com.squareup.okhttp:okhttp:2.7.5'
    testCompile 'com.squareup.okhttp:mockwebserver:2.7.5'

    runtime 'org.apache.ivy:ivy:2.2.0'
    runtime 'commons-logging:commons-logging:1.1.1'
    runtime 'commons-io:commons-io:2.2'
    runtime 'org.codehaus.groovy:groovy-all:2.4.7'
    runtime 'commons-lang:commons-lang:2.4'
    runtime 'com.google.guava:guava:18.0'
    runtime 'com.thoughtworks.xstream:xstream:1.3.1'
    runtime 'commons-codec:commons-codec:1.8'
    runtime 'com.fasterxml.jackson.core:jackson-core:2.9.3'
    runtime 'com.fasterxml.jackson.core:jackson-databind:2.9.3'
    runtime 'org.apache.commons:commons-compress:1.3'
    runtime 'org.apache.httpcomponents:httpclient:4.5.2'
    runtime 'org.apache.httpcomponents:httpcore:4.4.5'

    artifactory files("libs/build-info-api-2.9.2.jar",
            "libs/build-info-client-2.9.2.jar",
            "libs/build-info-extractor-2.9.2.jar",
            "libs/build-info-extractor-gradle-4.6.0.jar")

    compileOnly files("libs/build-info-api-2.9.2.jar",
            "libs/build-info-client-2.9.2.jar",
            "libs/build-info-extractor-2.9.2.jar",
            "libs/build-info-extractor-gradle-4.6.0.jar")
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://plugins.gradle.org/m2/")
    }
}

model {
    tasks.generatePomFileForIntershopMvnPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishIntershopMvnPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishIntershopMvnPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
